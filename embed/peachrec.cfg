// Edit these to customize the sound when recording is on standby, started, stopped, or bookmarked
alias pr_sound_start "play buttons/button17.wav"
alias pr_sound_stop "play buttons/button17.wav"
alias pr_sound_standby "play ui/mm_xp_chime.wav"
alias pr_sound_mark "play vo/heavy_goodjob03.mp3"

// Edit these to customize the message in chat when recording is started, stopped, or bookmarked
alias pr_message_start "say_team [PeachREC] Recording..."
alias pr_message_stop "say_team [PeachREC] Stop recording."
alias pr_message_mark "say_team [PeachREC] Bookmark."

// Additional PREC-like options using in-game Demo Support commands
// Enable these by uncommenting the line (removing the first "//")
//bind "UNBOUND" "pr_mark"    // Bind to bookmark the current tick for the demo being recorded.
//bind "UNBOUND" "ds_status"  // Bind to show the current recording status
//ds_notify 2                 // 0 - console message, 1 - start/stop/bookmark recording message in chat and console, 2 - show status on HUD and in console
//ds_min_streak 3             // Minimum kills for a killstreak to get logged
//ds_kill_delay 15            // Maximum time in seconds between kills to count towards a streak
//ds_dir "demos"              // Folder in /tf/ to store recorded demo files
//ds_sound 1                  // Play sound when start/stop recording (uses TF2 default sound)
//ds_screens 0                // Screenshots of status & end of round scoreboard
//ds_log 1                    // Log killstreak and bookmark events to an associated .txt file
//ds_autodelete 0             // Delete demo files without bookmarks and killstreaks
//ds_prefix ""                // Custom prefix for recordings





//============================//
//  PeachREC script contents  //
//   no reason to edit this   //
//============================//
alias peachrec "player_ready_toggle"

alias pr1 "alias peachrec pr2;alias pr_open pr_open_test;player_ready_toggle"
alias pr2 "pr_trigger"
alias pr_reset "alias peachrec pr1"
alias pr_trigger "alias peachrec;alias pr_close pr_end;pr_sound_start;pr_message_start;ds_record;echo =====PeachREC.started.recording====="
alias pr_end "ds_stop;alias peachrec player_ready_toggle;alias pr_open pr_open_init;alias pr_close pr_close_init;pr_sound_stop;pr_message_stop;echo =====PeachREC.stopped.recording====="

alias pr_open "pr_open_init"
alias pr_close "pr_close_init"

alias pr_open_init "alias peachrec pr1;alias pr_open pr_open_nat;alias pr_close pr_close_nat;player_ready_toggle;pr_sound_standby;echo =====PeachREC.waiting.for.match.to.start====="
alias pr_close_init "player_ready_toggle"

alias pr_open_nat "alias pr_close pr_close_nat"
alias pr_close_nat "alias pr_close pr_close_newserver"

alias pr_open_test "pr_reset;alias pr_open pr_open_nat;alias pr_close pr_close_nat;player_ready_toggle"

alias pr_close_newserver "alias peachrec player_ready_toggle;alias pr_open pr_open_init;alias pr_close pr_close_init;echo =====PeachREC.detected.new.server====="
alias pr2_newserver "alias pr2 pr_trigger;echo =====PeachREC.detected.new.match.server====="
alias pr_mvm "alias peachrec;echo =====PeachREC.detected.MvM.server====="

alias pr_mark "ds_mark;pr_message_mark;pr_sound_mark"

echo
echo
echo ===============
echo PeachREC Active
echo ===============
echo
echo